// Obliczanie liczb Fibonacciego za pomoc¹ potêgowania macierzy
// Data: 19.02.2012
// (C)2012 mgr Jerzy Wa³aszek
//-------------------------------------------------------------

program fib_matrix;

// Oblicza n-t¹ liczbê Fibonacciego
//---------------------------------
function fibo(n : integer) : qword;
var
  q11,q12,q21,q22 : qword; // macierz Q
  p11,p12,p21,p22 : qword; // macierz P
  w11,w12,w21,w22 : qword; // macierz W
begin
  if n < 2 then  fibo := n
  else
  begin

    // ustawiamy macierz Q

    q11 := 1; q12 := 1;
    q21 := 1; q22 := 0;

    // w macierzy W tworzymy macierz jednostkow¹

    w11 := 1; w12 := 0;
    w21 := 0; w22 := 1;

    dec(n);      // bêdzie nam potrzebna n-1 potêga Q

    while n > 0 do
    begin

      if (n and 1) = 1 then
      begin

        // wykonujemy mno¿enie P = W x Q

        p11 := w11*q11 + w12 * q21; p12 := w11*q12 + w12 * q22;
        p21 := w21*q11 + w22 * q21; p22 := w21*q12 + w22 * q22;

        // wynik przenosimy: W = P

        w11 := p11; w12 := p12;
        w21 := p21; w22 := p22;

      end;

      n := n shr 1;    // usuwamy z n sprawdzony bit

      if n = 0 then break;

      // podnosimy Q do kwadratu:  P = Q x Q

      p11 := q11*q11 + q12 * q21; p12 := q11*q12 + q12 * q22;
      p21 := q21*q11 + q22 * q21; p22 := q21*q12 + q22 * q22;

      // wynik przenosimy: Q = p

      q11 := p11; q12 := p12;
      q21 := p21; q22 := p22;

    end;

    fibo := w11;

  end;

end;

// Program g³ówny
//---------------

var
  i : integer;

begin
  for i := 0 to 93 do writeln('F(',i,') = ',fibo(i));
end.