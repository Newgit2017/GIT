// Zliczanie elementów listy
// Data: 19.02.2012
// (C)2012 mgr Jerzy Wa³aszek
//---------------------------

program slist_count;

// Typ elementów listy
//--------------------
type
  PslistEl = ^slistEl;

  slistEl =  record
    next  : PslistEl;
    data  : char;
  end;

// Definicja typu obiektowego slist
//---------------------------------

  slist = object
    public
      head : PslistEl;  // pocz¹tek listy

      constructor init;
      destructor  destroy;
      procedure   printl;
      procedure   push_front(v : char);
      procedure   pop_front;
      function    countv(v : char) : cardinal;
  end;

//---------------------
// Metody obiektu slist
//---------------------

// Konstruktor - inicjuje pole head
//---------------------------------
constructor slist.init;
begin
  head := nil;
end;

// Destruktor - usuwa listê z pamiêci
//-----------------------------------
destructor slist.destroy;
begin
  while head <> nil do pop_front;
end;

// Procedura wyœwietla zawartoœæ elementów listy
//----------------------------------------------
procedure slist.printl;
var
  p : PslistEl;
begin
  p := head;
  while p <> nil do
  begin
    write(p^.data);
    p := p^.next;
  end;
  writeln;
end;

// Procedura do³¹czania na pocz¹tek listy
//---------------------------------------
procedure slist.push_front(v : char);
var
  p : PslistEl;
begin
  new(p);
  p^.next := head;
  p^.data := v;
  head    := p;
end;

// Procedura usuwa pierwszy element
//---------------------------------
procedure slist.pop_front;
var
  p : PslistEl;
begin
  p := head;
  if p <> nil then
  begin
    head := p^.next;
    dispose(p);
  end;
end;

// Oblicza liczbê elementów zawieraj¹cych v
//-----------------------------------------
function slist.countv(v : char) : cardinal;
var
  p : PslistEl;
  c : cardinal;
begin
  c := 0;
  p := head;
  while p <> nil do
  begin
    if p^.data = v then inc(c);
    p := p^.next;
  end;
  countv := c;
end;

//---------------
// Program g³ówny
//---------------

var
  L : slist;    // obiekt listy jednokierunkowej
  i : integer;

begin
  Randomize;

  L.init;  // inicjujemy obiekt

  for i := 1 to 400 do L.push_front(char(65+random(26)));
  L.printl;

  for i := ord('A') to ord('Z') do writeln(char(i),' : ',L.countv(char(i)):3);

  L.destroy;
end. 