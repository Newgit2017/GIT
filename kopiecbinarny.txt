// Tworzenie kopca w tablicy
// Data: 27.02.2013
// (C)2013 mgr Jerzy Wa³aszek
//------------------------------

program create_heap;

// Zmienne globalne

var
  cr,cl,cp : string;            // ³añcuchy do znaków ramek
  T : array [0..14] of integer; // kopiec
  n : integer;                  // liczba wêz³ów

// Procedura wypisuje drzewo
//--------------------------
procedure printBT(sp,sn : string; v : integer);
var
  s : string;
begin
  if v < n then
  begin
    s := sp;
    if sn = cr then s[length(s) - 1] := ' ';
    printBT(s+cp,cr,2 * v + 2);

    s := Copy(sp,1,length(sp)-2);
    writeln(s,sn,T[v]);

    s := sp;
    if sn = cl then s[length(s) - 1] := ' ';
    printBT(s+cp,cl,2 * v + 1);
  end;
end;

// procedura wstawia v do kopca
//-----------------------------
procedure heap_push(v : integer);
var
  i,j : integer;
begin
  i := n;
  inc(n);
  j := (i - 1) div 2;

  while (i > 0) and (T[j] < v) do
  begin
    T[i] := T[j];
    i := j;
    j := (i - 1) div 2;
  end;

  T[i] := v;
end;

// **********************
// *** PROGRAM G£ÓWNY ***
// **********************

var
  i, v : integer;
begin
  // ustawiamy ³añcuchy znakowe, poniewa¿ nie wszystkie edytory pozwalaj¹
  // wstawiaæ znaki konsoli do tworzenia ramek.
  // cr = +--
  //      |

  // cl = |
  //      +--

  // cp = |
  //      |

  cr := #218#196;
  cl := #192#196;
  cp := #179#32;

  randomize;

  n := 0;

  for i := 1 to 15 do
  begin
    v := random(100);
    write(v,' ');
    heap_push(v);
  end;

  writeln;
  writeln;

  printBT('','',0);
end.